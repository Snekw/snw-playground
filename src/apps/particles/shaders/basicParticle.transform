#version 300 es
#define POS_LOCATION 0
#define VEL_LOCATION 1

layout(location = POS_LOCATION) in vec4 a_position;
layout(location = VEL_LOCATION) in vec2 a_velocity;

flat out vec2 v_position;
flat out vec2 v_velocity;

uniform vec2 u_mouse_location;
uniform float u_repel;
uniform float u_delta;

float PARTICLE_MASS = 1.0;
float GRAVITY_CENTER_MASS = 100.0;
float DAMPING = 1e-6;
void main() { 
    vec2 gravityCenter = u_mouse_location;

    float r = distance(a_position.xy, gravityCenter);
    vec2 direction = gravityCenter - a_position.xy;
    float force = PARTICLE_MASS * GRAVITY_CENTER_MASS / (r * r) * DAMPING;

    if(u_repel > 0.5){
        force = -force;
    }

    vec2 acceleration = force / PARTICLE_MASS * direction;
    vec2 newVelocity = a_velocity + acceleration;
    vec2 newPosition = a_position.xy + newVelocity * u_delta;
    v_velocity = newVelocity * 0.99;
    v_position = newPosition;
    // bounce at borders
    if (v_position.x > 1.0 || v_position.x < -1.0) {
        v_velocity.x *= -0.5;
    }
    if (v_position.y > 1.0 || v_position.y < -1.0) {
        v_velocity.y *= -0.5;
    }
    
    gl_Position = a_position;
    gl_PointSize = 1.0;
}
